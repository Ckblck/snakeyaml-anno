apply plugin: 'java'
apply plugin: 'jacoco'

group = 'de.beosign'
version = '1.3.0'

check.dependsOn jacocoTestReport

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileJava {
    doFirst {
        println "Using Java Version for compilation: " + JavaVersion.current()
    }
}

repositories {
    mavenCentral()
} 


sourceCompatibility=1.8
targetCompatibility=1.8

dependencies {
    //gradle maven plugin is not translating "+" to [X,)
    implementation 'org.yaml:snakeyaml:1.29'
    implementation 'org.apache.commons:commons-lang3:[3,4)'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:[5.8,5.9)'
	testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.slf4j:slf4j-api:[1.7,1.8)'
    testImplementation 'org.apache.logging.log4j:log4j-api:[2.15.0,3)'
    testImplementation 'org.apache.logging.log4j:log4j-core:[2.15.0,3)'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:[2.15.0,3)'
    testImplementation 'commons-io:commons-io:[2,3)'
    
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:[5.8,5.9)';
}

test {
  useJUnitPlatform()
}

jar {
    into ('META-INF') {
        from ('.') {
            include 'README.md','LICENCE.txt','LICENCE-3RD-PARTY.txt'
        }
    }
    manifest {
        attributes 'Implementation-Title': 'Snakeyaml-Anno', 'Implementation-Version': version
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts { // invoke with 'gradle build'
    archives sourcesJar
    archives javadocJar
}
